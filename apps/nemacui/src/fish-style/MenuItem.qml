import QtQuick 2.12
import QtQuick.Templates 2.12 as T
import QtQuick.Controls 2.12
import QtQuick.Controls.impl 2.12

import NemacUI 1.0 as NemacUI

T.MenuItem
{
    id: control

    property color hoveredColor: NemacUI.Theme.darkMode ? Qt.rgba(255, 255, 255, 0.2)
                                                       : Qt.rgba(0, 0, 0, 0.1)
    property color pressedColor: NemacUI.Theme.darkMode ? Qt.rgba(255, 255, 255, 0.1)
                                                       : Qt.rgba(0, 0, 0, 0.2)

    implicitWidth: Math.max(implicitBackgroundWidth + leftInset + rightInset,
                            implicitContentWidth + leftPadding + rightPadding)
    implicitHeight: Math.max(implicitBackgroundHeight + topInset + bottomInset,
                             implicitContentHeight + topPadding + bottomPadding,
                             implicitIndicatorHeight + topPadding + bottomPadding)

    verticalPadding: NemacUI.Units.smallSpacing
    hoverEnabled: true
    topPadding: NemacUI.Units.smallSpacing
    bottomPadding: NemacUI.Units.smallSpacing

    icon.width: NemacUI.Units.iconSizes.medium
    icon.height: NemacUI.Units.iconSizes.medium

    icon.color: control.enabled ? (control.highlighted ? control.NemacUI.Theme.highlightColor : control.NemacUI.Theme.textColor) :
                             control.NemacUI.Theme.disabledTextColor

    contentItem: IconLabel {
        readonly property real arrowPadding: control.subMenu && control.arrow ? control.arrow.width + control.spacing : 0
        readonly property real indicatorPadding: control.checkable && control.indicator ? control.indicator.width + control.spacing : 0
        leftPadding: !control.mirrored ? indicatorPadding + NemacUI.Units.smallSpacing * 2 : arrowPadding
        rightPadding: control.mirrored ? indicatorPadding : arrowPadding + NemacUI.Units.smallSpacing * 2

        spacing: control.spacing
        mirrored: control.mirrored
        display: control.display
        alignment: Qt.AlignLeft

        icon: control.icon
        text: control.text
        font: control.font
        color: control.enabled ? control.pressed || control.hovered ? control.NemacUI.Theme.textColor : 
               NemacUI.Theme.textColor : control.NemacUI.Theme.disabledTextColor
    }

    background: Rectangle {
        implicitWidth: 200
        implicitHeight: control.visible ? NemacUI.Units.gridUnit + NemacUI.Units.largeSpacing : 0
        radius: NemacUI.Theme.mediumRadius
        opacity: 1

        anchors {
            fill: parent
            leftMargin: NemacUI.Units.smallSpacing
            rightMargin: NemacUI.Units.smallSpacing
        }

        color: control.pressed || highlighted ? control.pressedColor : control.hovered ? control.hoveredColor : "transparent"
    }
}
